use uo;
use os;
use cfgfile;
use util;

include ":itemutils:objtype";
//include "include/OLD/sound";
include ":itemutils:canAccess";
include ":attributes:attributeConstants";
include ":attributes:attributes";
include "include/say";
include ":charactercreation:habilidades";

program skill_lockpicking( me, lockpick )

	EraseObjProperty(me, "IsMeditating");
	EraseObjProperty(me, "HealTimer");

	if (!maolivre(me, 2))
		return 0;
	endif

	if(!can_access(me, lockpick))
		return;
	endif

	if(lockpick.movable == 0)
		SendSysMessageEx(me, "Voce nao pode usar isto.", SSM_FAIL);
		return;
	endif

	if (!ReserveItem(lockpick))
		return;
	endif
	SendSysMessageEx(me, "O que voce deseja fazer com isto?", SSM_REQUEST);
	var chest := Target(me);
	if (!chest)
		SendSysMessageEx(me,"Cancelado.", SSM_FAIL);
		return;
	endif
	if ( (!Accessible(me,chest)) || (!Accessible(me,lockpick)) )
		SendSysMessageEx(me, "Voce nao alcanca o alvo!", SSM_FAIL);
		return;
	endif
	if ( Distance(me, chest) > 2)
		SendSysMessageEx(me, "Voce esta muito distante.", SSM_FAIL);
		return;
	endif

	if (GetObjProperty(chest,"level"))
		if(valorInicial(me, AWARENESS) >= 40)
			if(me.hidden)
				SendSysMessageEx(me, "*Arrombando a fechadura*", SSM_INFO);
			else
				PrintTextAbove(me, "*Arrombando a fechadura*");
			endif
		else
			PrintTextAbove(me, "*Arrombando a fechadura*");
		endif
		if (!Wait(me, 5))
			SendSysMessageEx(me, "Voce interrompeu o processo.", SSM_FAIL);
			return;
		endif
		PickTreasureChest(me, lockpick, chest); //funciona tanto pra chests quando pra portas
	else
		SendSysMessageEx(me, "Esse nao e um alvo valido.", SSM_FAIL);
		ReleaseItem(lockpick);
	endif

endprogram

function PickTreasureChest(me, lockpick, chest)

	var lvl := CInt(GetObjProperty(chest,"level"));
	if (!chest.locked)
		SendSysMessageEx(me,"O alvo nao parece estar tracado.", SSM_INFO);
		return;
	endif
	if(valorInicial(me, AWARENESS) >= 40)
		if(me.hidden)
			SendSysMessageEx(me, "*Arrombando a fechadura silenciosamente*", SSM_INFO);
		else
			PrintTextAbove(me, "*Arrombando a fechadura silenciosamente*");
		endif
	else
		PrintTextAbove(me, "*Arrombando a fechadura*");
		PlaySoundEffect(chest, 0x0242);
	endif
	sleepms(250);
	var diff;
	case (lvl)
		1: diff := 15;
		2: diff := 40;
		3: diff := 60;
		4: diff := 86;
		5: diff := 90;
		6: diff := 110;
		default: diff := 40;
	endcase
  var diffmod := Cint(GetObjProperty(me, "conhecimentosobrefechaduras"))*5;
  diff := diff - diffmod;
  if (diff <= 0)
	diff := 5;
  endif
  var check := SkillCheck(me, MECHANICALAPITUDE, diff);

  if ( check > 0 )
	if(valorInicial(me, AWARENESS) >= 40)
		SendSysMessageEx(me, "*Destrancou silenciosamente*", SSM_INFO);
		PrintTextAbovePrivate(chest, "*$*", me);
	else
		PrintTextAbove(me, "*Destrancou*");
		PlaySoundEffect(chest, 0x0200);
	endif
	set_critical(1);
	SetObjProperty(chest, "arrombado", "" + me.name);
	SetObjProperty(chest, "locktime", ReadGameClock());
  	chest.locked := 0;
  	set_critical(0);
  else
  	if(me.hidden)
  		me.hidden := 0;
  	endif
  	PrintTextAbove(me, "*Falhou em arrombar*");
	if (!temHabilidade(me, "Especialista em Invasoes") && check < -20)
  		PlaySoundEffect(chest,0xef);
		SendSysMessageEx(me,"Sua gazua quebrou!", SSM_FAIL);
		SubtractAmount(lockpick,1);
		return;
	endif
  endif
  ReleaseItem(lockpick);
endfunction
